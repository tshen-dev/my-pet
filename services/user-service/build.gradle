plugins {
  id 'org.springframework.boot' version '2.7.8'
  id 'io.spring.dependency-management' version '1.0.13.RELEASE'
  id "org.springframework.cloud.contract" version "3.1.5"
  id 'java'
  id 'maven-publish'
}

group = 'com.tshen.pet'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'
def clientJarName = rootProject.name + '-client'

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    name = "GitHubPackages"
    url = uri("https://maven.pkg.github.com/tshen-dev/my-pet")
    credentials {
      username = System.getenv('GITHUB_USER')
      password = System.getenv('GITHUB_REPO_TOKEN')
    }
  }
}

tasks.register('clientJar', Jar) {
  dependsOn classes
  shouldRunAfter {build}
  archiveBaseName.set(clientJarName)
  version = project.version
  includeEmptyDirs = false
  from sourceSets.main.output
  include("com\\tshen\\pet\\user\\client\\**")
}

tasks.register('stubsJar', Jar) {
  into("/") {
    include('**/*.groovy')
    from("${project.rootDir}/src/main/resources/contracts")
  }
}

publishing {
  repositories {
    maven {
      name = "GitHubPackages"
      url = uri("https://maven.pkg.github.com/tshen-dev/my-pet")
      credentials {
        username = System.getenv('GITHUB_USER')
        password = System.getenv('GITHUB_REPO_TOKEN')
      }
    }
  }
  publications {
    publishClientJar(MavenPublication) {
      artifact clientJar
      groupId group
      artifactId clientJarName
      version project.version
    }
    publishStubsJar(MavenPublication) {
      artifactId "${project.name}"
      artifact verifierStubsJar
    }
  }
}

dependencies {
  apply from: "../dependencies.gradle"

  implementation project(":my-pet-utils")

  implementation libs.springStarter
  implementation libs.springStarterWeb
  implementation libs.springStarterConfig

  implementation libs.keycloakSpringBootStarter
  implementation libs.keycloakAdminClient

  implementation libs.mapstruct

  implementation libs.springDataJpa
  implementation libs.postgresql
  implementation libs.liquibase

  implementation libs.springOpenfeign

  implementation libs.springKafka

  implementation "com.tshen.pet:notification-service-client:+"

  developmentOnly devLibs.springDevTools

  compileOnly libs.lombok

  annotationProcessor libs.lombok
  annotationProcessor libs.mapstructProcessor

  testImplementation testLibs.springBootStarterTest
  testImplementation testLibs.h2database
  testImplementation testLibs.springContractVerifier
  testImplementation testLibs.springContractWiremock
  testImplementation testLibs.springContractStubRunner
  testImplementation("com.tshen.pet:notification-service:+:stubs") {
    transitive = false
  }
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
}

test {
  useJUnitPlatform()
}

contracts {
  contractsDslDir = file("src/test/resources/contracts")
  testFramework = "JUNIT5"
  baseClassForTests = "com.tshen.pet.user.contracts.ContractVerifierBase"
}

contractTest {
  useJUnitPlatform()
  testLogging {
    exceptionFormat = 'full'
  }
  afterSuite { desc, result ->
    if (!desc.parent) {
      println "Results: (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
      boolean skipTests = Boolean.parseBoolean(project.findProperty('SKIP_TESTS') ?: "false" as String)
      if (result.testCount == 0 && !skipTests) {
        throw new IllegalStateException("No tests were found. Failing the build")
      }
    }
  }
}
